/***************************************************************

JEP - Java Math Expression Parser 2.3.0
      October 3 2004
      (c) Copyright 2004, Nathan Funk and Richard Morris
      See LICENSE.txt for license information.

This file contains the grammar from which the parser is
generated. The parser generation tools being used are JavaCC
and its component JJTree. JJTree processes the .jjt file and
generates the .jj file. This file is then used be JavaCC to
generate the final java classes.

Knowledge of parser generation is definitely beneficial if you
are planning to make changes to this file. Although many parts
are self explanatory, it is helpful to know how the grammar is
handled by JJTree and JavaCC.

You can follow the structure of the grammar by starting in the
line which reads "ASTStart Start() #Start : {}". From there you
will be able to trace the same path the parser follows when
parsing an expression.

The concept of precedence is very important. Since this is a
recursive descent parser, it is easy to see which operators have
higher order of precedence. The following table outlines the
order incorporated in this grammar.

Order of precedence (from lowest to highest):

   ||               or  (logical)
   &&               and (logical)
   ==  !=           equal, not equal
   <   >   <=  >=   less, greater, less or equal, greater or equal
   +   -            addition, subtraction
   *   /   %        multiplication, division, modulus
   !   +   -        not, unary +/-
   ^                power

The order of precedence is greatly inspired by the operator
precedence of the C language. See "The C Programming Language"
Kernighan & Richie 2nd Ed p.53. Java also uses a very similar
precedence. Note that the '^' operator is used for bitwise 
exclusive OR in Java and C, but represents the power operator
in this grammar.

***************************************************************/



/***************************************************************
OPTIONS
***************************************************************/
options {
	JAVA_UNICODE_ESCAPE = true;
	MULTI = true;
	VISITOR = true;
	VISITOR_EXCEPTION = "ParseException";
	NODE_DEFAULT_VOID = true;
//	NODE_PACKAGE = "org.nfunk.jep.parser";
//	BUILD_NODE_FILES=false;
	STATIC = false;
//	DEBUG_TOKEN_MANAGER = true;
}


/***************************************************************
PARSER BEGIN
***************************************************************/

PARSER_BEGIN(Parser)
package org.nfunk.jep;

import org.nfunk.jep.function.PostfixMathCommandI;

public class Parser {
	private JEP jep;
	private SymbolTable symTab;
	private OperatorSet opSet;
	private int initialTokenManagerState = DEFAULT;
	
	public Node parseStream(java.io.Reader stream, JEP jep_in) throws ParseException {
		restart(stream,jep_in);
		// Parse the expression, and return the enable_tracing();
		Node node = Start();
		if(node == null) throw new ParseException(JEP.getMessage("JEP.ERR.NOEXPR")); // modified by lm
		return node.jjtGetChild(0);
	}

	/**
	 * Restart the parse with the given stream.
	 * @since 2.3.0 beta 1
	 */
	public void restart(java.io.Reader stream, JEP jep_in) {
		ReInit(stream);
   		this.token_source.SwitchTo(initialTokenManagerState);
		jep = jep_in;
		symTab = jep.getSymbolTable();
		opSet = jep.getOperatorSet();
	}

	/**
	 * Continue parsing without re-initialising stream.
	 * Allows reentrancy of parser so that strings like
	 * "x=1; y=2; z=3;" can be parsed.
	 * When a semi colon is encountered parsing finishes leaving the rest of the string unparsed.
	 * Parsing can be resumed from the current position by using this method.
	 * For example
	 * <pre>
	 * XJep j = new XJep();
	 * Parser parse = j.getParse();
	 * StringReader sr = new StringReader("x=1; y=2; z=3;");
	 * parse.restart(sr,j);
	 * Node node;
	 * try {
	 * while((node = j.continueParse())!=null) {
	 *    j.println(node);
	 * } }catch(ParseException e) {}
	 * </pre>
	 */
	public Node continueParse() throws ParseException {
		ASTStart node = Start();
		if(node==null) return null;
		return node.jjtGetChild(0);
	}

	private void addToErrorList(String errorStr) {
		jep.errorList.addElement(errorStr);		
	}

	/**
	 * Sets the initial state that the token manager is in.
	 * Can be used to change how x.x is interpreted, either as a single
	 * identifier (DEFAULT) or as x <DOT> x (NO_DOT_IN_IDENTIFIERS)
	 * @param state the state to be in. Currently the only legal values are DEFAULT and NO_DOT_IN_IDENTIFIER
	 */
	public void setInitialTokenManagerState(int state) {
		initialTokenManagerState = state;
	}

	/**
	 * Translate all escape sequences to characters. Inspired by Rob Millar's
	 * unescape() method in rcm.util.Str fron the Web Sphinx project.
	 *
	 * @param inputStr String containing escape characters.
	 * @return String with all escape sequences replaced.
	 */
	private String replaceEscape(String inputStr) {
		int len = inputStr.length();
		int p = 0;
		int i;
		String metachars = "tnrbf\\\"'";
		String chars = "\t\n\r\b\f\\\"'";
		StringBuffer output = new StringBuffer();
		while((i = inputStr.indexOf('\\', p)) != -1) {
			output.append(inputStr.substring(p, i));
			if(i+1 == len) break;
			// find metacharacter
			char metac = inputStr.charAt(i+1);
			// find the index of the metac
			int k = metachars.indexOf(metac);
			if(k == -1) {
				// didn't find the metachar, leave sequence as found.
				// This code should be unreachable if the parser
				// is functioning properly because strings containing
				// unknown escape characters should not be accepted.
				output.append('\\');
				output.append(metac);
			} else {
				// its corresponding true char
				output.append(chars.charAt(k));   
			}
			// skip over both escape character & metacharacter
			p = i + 2;
		}
		// add the end of the input string to the output
		if(p < len) output.append(inputStr.substring(p));
		return output.toString();
	}
}

PARSER_END(Parser)

/***************************************************************
SKIP
***************************************************************/

<*> SKIP :
{
  " "
  | "\t"
  | "\n"
  | "\r"

  | <"//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
  | <"/*" (~["*"])* "*" (~["/"] (~["*"])* "*")* "/">
}


/***************************************************************
TOKENS
***************************************************************/

<*> TOKEN : /* LITERALS */
{
	< INTEGER_LITERAL:
		<DECIMAL_LITERAL>
	>
  |
	< #DECIMAL_LITERAL: ["0"-"9"] (["0"-"9"])* >
  |
	< FLOATING_POINT_LITERAL:
		(["0"-"9"])+ "." (["0"-"9"])* (<EXPONENT>)?
		| "." (["0"-"9"])+ (<EXPONENT>)?
		| (["0"-"9"])+ <EXPONENT>
	>
  |
	< #EXPONENT: ["e","E"] (["+","-"])? (["0"-"9"])+ >
  |
	< STRING_LITERAL:
		"\""
		( (~["\"","\\","\n","\r"])
		| ("\\" ["n","t","b","r","f","\\","'","\""]	)
		)*
		"\""
	>
}

/* IDENTIFIERS 
	
	Letters before version 2.22
	< #LETTER: ["_","a"-"z","A"-"Z"] >

	In Ver 2.3.0.1 presence of . in an identifier is switchable.
	In the DEFAULT lexical state identifiers can contain a .
	In the NO_DOT_IN_IDENTIFIERS state identifiers cannot contain a .
	the state can be set by using
	Parser.setInitialTokenManagerState
*/

<DEFAULT> TOKEN:
{
	<IDENTIFIER1: <LETTER1>(<LETTER1>|<DIGIT1>|".")*>
  |
	< #LETTER1:
	[
		"\u0024",           // $
		"\u0041"-"\u005a",  // A - Z
		"\u005f",           // _
		"\u0061"-"\u007a",  // a - z
		"\u00c0"-"\u00d6",  // Upper case symbols of Latin-1 Supplement
		"\u00d8"-"\u00f6",  // Lower case symbols of Latin-1 Supplement
		"\u00f8"-"\u00ff",  // More lower case symbols of Latin-1 Supplement
		"\u0100"-"\u1fff",  // Many languages (including Greek)
		"\u3040"-"\u318f",  // Hiragana, Katakana, Bopomofo, Hangul Compatibility Jamo
		"\u3300"-"\u337f",  // CJK Compatibility
		"\u3400"-"\u3d2d",  // CJK Unified Ideographs Extension A
		"\u4e00"-"\u9fff",  // CJK Unified Ideographs
		"\uf900"-"\ufaff"   // CJK Compatibility Ideographs
	]
	>	
  |
	< #DIGIT1: ["0"-"9"] >
}

<NO_DOT_IN_IDENTIFIERS> TOKEN:
{
	<IDENTIFIER2: <LETTER2>(<LETTER2>|<DIGIT2>)*>
  |
	< #LETTER2:
	[
		"\u0024",           // $
		"\u0041"-"\u005a",  // A - Z
		"\u005f",           // _
		"\u0061"-"\u007a",  // a - z
		"\u00c0"-"\u00d6",  // Upper case symbols of Latin-1 Supplement
		"\u00d8"-"\u00f6",  // Lower case symbols of Latin-1 Supplement
		"\u00f8"-"\u00ff",  // More lower case symbols of Latin-1 Supplement
		"\u0100"-"\u1fff",  // Many languages (including Greek)
		"\u3040"-"\u318f",  // Hiragana, Katakana, Bopomofo, Hangul Compatibility Jamo
		"\u3300"-"\u337f",  // CJK Compatibility
		"\u3400"-"\u3d2d",  // CJK Unified Ideographs Extension A
		"\u4e00"-"\u9fff",  // CJK Unified Ideographs
		"\uf900"-"\ufaff"   // CJK Compatibility Ideographs
	]
	>	
  |
	< #DIGIT2: ["0"-"9"] >
}

/* OPERATORS */
<*> TOKEN:
{
    < ASSIGN: "=">  // rjm
  | < SEMI:	";"  >	// rjm
  |	< COMMA:","  >	// rjm
  |	< GT:   ">"  >
  |	< LT:   "<"  >
  |	< EQ:   "==" >
  |	< LE:   "<=" >
  |	< GE:   ">=" >
  |	< NE:   "!=" >
  |	< AND:  "&&" >
  |	< OR:   "||" >
  |	< PLUS: "+"  >
  |	< MINUS:"-"  >
  |	< MUL:  "*"  >
  |	< DOT:  "."  >	// rjm
  |	< DIV:  "/"  >
  |	< BDIV: "\\" >  // lm
  |	< PIPE: "|"  >  // lm
  |	< MOD:  "%"  >
  |	< NOT:  "!"  >
  |	< POWER:"^"  >
  |	< CROSS:"^^" >  // rjm
  |	< LSQ:	"["  >	// rjm
  |	< RSQ:	"]"  >	// rjm
  |	< LRND:	"("  >	// rjm
  |	< RRND:	")"  >	// rjm
  |	< COLON:":"  >	// lm
  |	< SHARP: "#" >	// lm
  |	< SSHARP:"##">  // lm
  |	< AT: "@"    >  // lm
  |	< X1: "&"    >  // lm
  |	< X2: "\""   >  // lm
  |	< QUOTE: "\'">  // lm
  |	< LCURLY: "{">  // lm
  |	< RCURLY: "}">  // lm
  |	< BLANK: "`" >	// lm (a metacharacter, to allow formatting)
  |	< NEWLINE:"~">	// lm (a metacharacter, to allow formatting)
}


/***************************************************************
GRAMMAR START
***************************************************************/

ASTStart Start() #Start : {}
{
	Expression()
		(			<SEMI> Expression()
		)*
		<EOF> // modified by lm
			{ return jjtThis; }
}


void Expression() : {}
{
	AndExpression()
		(
			( <OR> AndExpression()
				{ jjtThis.setOperator(opSet.getOr()); }
			) #FunNode(2)
		)*
}


void AndExpression() : {}
{
	EqualExpression()
		(
			( <AND> EqualExpression()
				{ jjtThis.setOperator(opSet.getAnd()); }
			) #FunNode(2)
		)*
}


void EqualExpression() : {}
{
	RelationalExpression()
		(
			( <NE> RelationalExpression()
				{ jjtThis.setOperator(opSet.getNE()); }
			) #FunNode(2)
			|
			( <EQ> RelationalExpression()
				{ jjtThis.setOperator(opSet.getEQ()); }
			) #FunNode(2)
		)*
}


void RelationalExpression() : {}
{
	AdditiveExpression()
		(
			( <LT> AdditiveExpression()
				{ jjtThis.setOperator(opSet.getLT()); }
			) #FunNode(2)
			|
			( <GT> AdditiveExpression()
				{ jjtThis.setOperator(opSet.getGT()); }
			) #FunNode(2)
			|
			( <LE> AdditiveExpression()
				{ jjtThis.setOperator(opSet.getLE()); }
			) #FunNode(2)
			|
			( <GE> AdditiveExpression()
				{ jjtThis.setOperator(opSet.getGE()); }
			) #FunNode(2)
		)*
}


void AdditiveExpression() : {}
{
	MultiplicativeExpression()
		(
			( <PLUS> MultiplicativeExpression()
				{ jjtThis.setOperator(opSet.getAdd()); }
			) #FunNode(2)
			|
			( <MINUS> MultiplicativeExpression()
				{ jjtThis.setOperator(opSet.getSubtract()); }
			) #FunNode(2)
		)*
}


void MultiplicativeExpression() : { Operator op = null; Operator op2 = null; }
{
	UnaryExpression()
		(
			( PowerExpression()
				{
					if(!jep.implicitMul) throw new ParseException(JEP.getMessage("JEP.ERR.IMPL_MULT")); // modified by lm
					jjtThis.setOperator(opSet.getMultiply());
				}
			) #FunNode(2)
			|
			( <MUL> UnaryExpression()
				{ jjtThis.setOperator(opSet.getMultiply()); }
			) #FunNode(2)
			|
			( <DOT> UnaryExpression()
				{ jjtThis.setOperator(opSet.getDot()); }
			) #FunNode(2)
			|
			( <CROSS> UnaryExpression()
				{ jjtThis.setOperator(opSet.getCross()); }
			) #FunNode(2)
			|
			( <DIV> UnaryExpression()
				{ jjtThis.setOperator(opSet.getDivide()); }
			) #FunNode(2)
			|
			( <MOD> UnaryExpression()
				{ jjtThis.setOperator(opSet.getMod()); }
			) #FunNode(2)
			|
			( <SHARP> UnaryExpression()	// added by lm
				{ jjtThis.setOperator(opSet.getConcatenate()); }
			) #FunNode(2)
			|
			( <SSHARP> UnaryExpression()	// added by lm
				{ jjtThis.setOperator(opSet.getDecatenate()); }
			) #FunNode(2)
		)*
}


void UnaryExpression() : { Operator op = null; Object dim = null; }
{
	LOOKAHEAD(<PLUS> UnaryExpression())
	( <PLUS> UnaryExpression() )
	|
	LOOKAHEAD(<MINUS> UnaryExpression())
	( <MINUS> UnaryExpression()
		{ jjtThis.setOperator(opSet.getUMinus()); }
	) #FunNode(1)
	|
	( <NOT> UnaryExpression()
		{ jjtThis.setOperator(opSet.getNot()); }
	) #FunNode(1)
	|
	( <AT> UnaryExpression()			// added by LM: monadic prefix operator
		{ jjtThis.setOperator(opSet.getSize()); }
	) #FunNode(1)
	|
	LOOKAHEAD(ModifiableOperator() <DIV> <LSQ> RealConstant() <RSQ> UnaryExpression())
	( op = ModifiableOperator()			// added by LM: APL-style reduction-with-dim operator
	  <DIV> <LSQ> dim = RealConstant() <RSQ> 
		{
			jjtThis.setOperator(op);
			ASTConstant leftarg = new ASTConstant(JJTCONSTANT);
			leftarg.setValue("/");
			jjtree.pushNode(leftarg);
			ASTConstant leftarg2 = new ASTConstant(JJTCONSTANT);
			leftarg2.setValue(dim);
			jjtree.pushNode(leftarg2);
		} UnaryExpression()
	) #FunNode(3)
	|
	LOOKAHEAD(ModifiableOperator() <PIPE> <LSQ> RealConstant() <RSQ> UnaryExpression())
	( op = ModifiableOperator()			// added by LM: APL-style pair-scan-with-dim operator
	  <PIPE> <LSQ> dim = RealConstant() <RSQ> 
		{
			jjtThis.setOperator(op);
			ASTConstant leftarg = new ASTConstant(JJTCONSTANT);
			leftarg.setValue("|");
			jjtree.pushNode(leftarg);
			ASTConstant leftarg2 = new ASTConstant(JJTCONSTANT);
			leftarg2.setValue(dim);
			jjtree.pushNode(leftarg2);
		} UnaryExpression()
	) #FunNode(3)
	|
	LOOKAHEAD(ModifiableOperator() <BDIV> <LSQ> RealConstant() <RSQ> UnaryExpression())
	( op = ModifiableOperator()			// added by LM: APL-style scan-with-dim operator
	  <BDIV> <LSQ> dim = RealConstant() <RSQ> 
		{
			jjtThis.setOperator(op);
			ASTConstant leftarg = new ASTConstant(JJTCONSTANT);
			leftarg.setValue("\\");
			jjtree.pushNode(leftarg);
			ASTConstant leftarg2 = new ASTConstant(JJTCONSTANT);
			leftarg2.setValue(dim);
			jjtree.pushNode(leftarg2);
		} UnaryExpression()
	) #FunNode(3)
	|
	LOOKAHEAD(ModifiableOperator() <DIV> UnaryExpression())
	( op = ModifiableOperator()			// added by LM: APL-style reduction operator
		{
			jjtThis.setOperator(op);
			ASTConstant leftarg = new ASTConstant(JJTCONSTANT);
			leftarg.setValue("/");
			jjtree.pushNode(leftarg);
		} <DIV> UnaryExpression()
	) #FunNode(2)
	|
	LOOKAHEAD(ModifiableOperator() <PIPE> UnaryExpression())
	( op = ModifiableOperator()			// added by LM: APL-style pair-scan operator
		{
			jjtThis.setOperator(op);
			ASTConstant leftarg = new ASTConstant(JJTCONSTANT);
			leftarg.setValue("|");
			jjtree.pushNode(leftarg);
		} <PIPE> UnaryExpression()
	) #FunNode(2)
	|
	LOOKAHEAD(ModifiableOperator() <BDIV> UnaryExpression())
	( op = ModifiableOperator()			// added by LM: APL-style scan operator
		{
			jjtThis.setOperator(op);
			ASTConstant leftarg = new ASTConstant(JJTCONSTANT);
			leftarg.setValue("\\");
			jjtree.pushNode(leftarg);
		} <BDIV> UnaryExpression()
	) #FunNode(2)
	|
	PowerExpression()
}


Operator ModifiableOperator() : {}		// added by LM: for APL-style operators
{
	( <PLUS>
		{ return opSet.getAdd(); }
	)
	|
	( <MUL>
		{ return opSet.getMultiply(); }
	)
	|
	( <MINUS>
		{ return opSet.getSubtract(); }
	)
	|
	( <DIV>
		{ return opSet.getDivide(); }
	)
	|
	( <MOD>
		{ return opSet.getMod(); }
	)
	|
	( <POWER>
		{ return opSet.getPower(); }
	)
	|
	( <AND>
		{ return opSet.getAnd(); }
	)
	|
	( <OR>
		{ return opSet.getOr(); }
	)
	|
	( <GT>
		{ return opSet.getGT(); }
	)
	|
	( <LT>
		{ return opSet.getLT(); }
	)
		|
	( <EQ>
		{ return opSet.getEQ(); }
	)
		|
	( <LE>
		{ return opSet.getLE(); }
	)
		|
	( <GE>
		{ return opSet.getGE(); }
	)
		|
	( <NE>
		{ return opSet.getNE(); }
	)
}


void PowerExpression() : {}
{
	UnaryExpressionNotPlusMinus()
		[
			( <POWER> UnaryExpression()
				{ jjtThis.setOperator(opSet.getPower()); }
			) #FunNode(2)
		]
}


void UnaryExpressionNotPlusMinus() : {}
{
	AnyConstant()
	|
	( LOOKAHEAD({
		(
			(getToken(1).kind == IDENTIFIER1 || getToken(1).kind == IDENTIFIER2)
				&&		// modified by lm, to deal with custom functions
			(jep.funTab.containsKey(getToken(1).image) || (jep.symTab.containsKey(getToken(1).image) && getToken(1).image.startsWith("_") && !getToken(1).image.startsWith("__")))
		)
				})
		Function()
		|
		Variable()
	)
	|
	<LRND> Expression() <RRND>
	|
	ListExpression()
	|
	DefinedExpression()
}


void ListExpression() #FunNode : {} 	// modified by lm, to deal with [x:y] and [x:y:z] lists
{
	LOOKAHEAD(50)
	( <LSQ> [ Expression() ( <COMMA> Expression() )* ] <RSQ>	// modified by lm, to allow empty ([]) lists
		{ jjtThis.setOperator(opSet.getList()); }
	)
	|
	LOOKAHEAD(50)
	( <LSQ> Expression() <COLON> Expression() <COLON> Expression() <RSQ>
		{ jjtThis.setOperator(opSet.getList2()); }
	)
	|
	LOOKAHEAD(50)
	( <LSQ> Expression() <COLON> Expression() <RSQ>
		{ jjtThis.setOperator(opSet.getList2()); }
	)
}


void DefinedExpression() #FunNode : {} 	// modified by lm, to deal with {expr} lists
{
	( <LCURLY> Expression() <RCURLY>
		{ jjtThis.setOperator(opSet.getDefExpr()); }
	)
}


void Variable() : { String identString = ""; }	 	// modified by lm, to deal with list selectors
{
	LOOKAHEAD(50)
	( identString = Identifier()
		{
			if(!symTab.containsKey(identString)) {
				addToErrorList(JEP.getMessage("JEP.ERR.UNREC_LIST") + ": \"" + identString +"\""); // modified by lm
			}
		}
		<LSQ> ListSelector(identString) <RSQ>
	)
	|
	( identString = Identifier()
		{
			if(symTab.containsKey(identString)) {
				jjtThis.setVar(symTab.getVar(identString));
			} else {
				if(jep.allowUndeclared) {
					jjtThis.setVar(symTab.makeVarIfNeeded(identString));
				} else {
					addToErrorList(JEP.getMessage("JEP.ERR.UNREC_VAR") + ": \"" + identString +"\""); // modified by lm
				}
			}
		}
	) #VarNode
}


void ListSelector(String listName) : {}	 	// added by lm: Python-style list selectors
{
	(					// list[index1,index2,...]
		{
			jjtThis.setFunction("get", (PostfixMathCommandI)jep.funTab.get("get"));
			ASTVarNode leftarg = new ASTVarNode(JJTVARNODE);
			leftarg.setVar(symTab.getVar(listName));
			jjtree.pushNode(leftarg);
		}
	Expression() ( <COMMA> Expression() )*
	) #FunNode
}


void Function() : { int reqArguments = 0; String identString = ""; Object dim = null; }
{
	LOOKAHEAD(20)
	( identString = Identifier()
		{
			if(jep.funTab.containsKey(identString)) {
				reqArguments = ((PostfixMathCommandI)jep.funTab.get(identString)).getNumberOfParameters();
				jjtThis.setFunction(identString, (PostfixMathCommandI)jep.funTab.get(identString));
			} else if(jep.symTab.containsKey(identString)) { // added by lm: custom (user-defined) function
				reqArguments = -1;
				jjtThis.setFunction("exec", (PostfixMathCommandI)jep.funTab.get("exec"));
				ASTVarNode leftarg = new ASTVarNode(JJTVARNODE);
				leftarg.setVar(symTab.getVar(identString));
				jjtree.pushNode(leftarg);
			} else {
				addToErrorList(JEP.getMessage("JEP.ERR.UNREC_FUN") + ": \"" + identString + "\""); // modified by lm
			}
		}
		<LRND> ArgumentList(reqArguments, identString) <RRND>
	) #FunNode
	|
	LOOKAHEAD(Identifier() <DIV> <LSQ> RealConstant() <RSQ> UnaryExpression())
	( identString = Identifier()			// added by LM: APL-style reduction-with-dim operator
	  <DIV> <LSQ> dim = RealConstant() <RSQ> 
		{
			reqArguments = ((PostfixMathCommandI)jep.funTab.get(identString)).getNumberOfParameters();
			jjtThis.setFunction(identString, (PostfixMathCommandI)jep.funTab.get(identString));
			ASTConstant leftarg = new ASTConstant(JJTCONSTANT);
			leftarg.setValue("/");
			jjtree.pushNode(leftarg);
			ASTConstant leftarg2 = new ASTConstant(JJTCONSTANT);
			leftarg2.setValue(dim);
			jjtree.pushNode(leftarg2);
		} <LRND> ArgumentList(reqArguments, identString) <RRND>
	) #FunNode
	|
	LOOKAHEAD(Identifier() <PIPE> <LSQ> RealConstant() <RSQ> UnaryExpression())
	( identString = Identifier()			// added by LM: APL-style pair-scan-with-dim operator
	  <PIPE> <LSQ> dim = RealConstant() <RSQ> 
		{
			reqArguments = ((PostfixMathCommandI)jep.funTab.get(identString)).getNumberOfParameters();
			jjtThis.setFunction(identString, (PostfixMathCommandI)jep.funTab.get(identString));
			ASTConstant leftarg = new ASTConstant(JJTCONSTANT);
			leftarg.setValue("|");
			jjtree.pushNode(leftarg);
			ASTConstant leftarg2 = new ASTConstant(JJTCONSTANT);
			leftarg2.setValue(dim);
			jjtree.pushNode(leftarg2);
		} <LRND> ArgumentList(reqArguments, identString) <RRND>
	) #FunNode
	|
	LOOKAHEAD(Identifier() <BDIV> <LSQ> RealConstant() <RSQ> UnaryExpression())
	( identString = Identifier()			// added by LM: APL-style scan-with-dim operator
	  <BDIV> <LSQ> dim = RealConstant() <RSQ> 
		{
			reqArguments = ((PostfixMathCommandI)jep.funTab.get(identString)).getNumberOfParameters();
			jjtThis.setFunction(identString, (PostfixMathCommandI)jep.funTab.get(identString));
			ASTConstant leftarg = new ASTConstant(JJTCONSTANT);
			leftarg.setValue("\\");
			jjtree.pushNode(leftarg);
			ASTConstant leftarg2 = new ASTConstant(JJTCONSTANT);
			leftarg2.setValue(dim);
			jjtree.pushNode(leftarg2);
		} <LRND> ArgumentList(reqArguments, identString) <RRND>
	) #FunNode
	|
	LOOKAHEAD(20)							// added by LM: APL-style reduction operator
	( identString = Identifier()
		{
			if(jep.funTab.containsKey(identString)) {
				reqArguments = ((PostfixMathCommandI)jep.funTab.get(identString)).getNumberOfParameters();
				jjtThis.setFunction(identString, (PostfixMathCommandI)jep.funTab.get(identString));
				ASTConstant leftarg = new ASTConstant(JJTCONSTANT);
				leftarg.setValue("/");
				jjtree.pushNode(leftarg);
			} else {
				addToErrorList(JEP.getMessage("JEP.ERR.UNREC_FUN") + ": \"" + identString + "\"");
			}
		}
		<DIV> <LRND> ArgumentList(reqArguments, identString) <RRND>
	) #FunNode
	|
	LOOKAHEAD(20)							// added by LM: APL-style pair-scan operator
	( identString = Identifier()
		{
			if(jep.funTab.containsKey(identString)) {
				reqArguments = ((PostfixMathCommandI)jep.funTab.get(identString)).getNumberOfParameters();
				jjtThis.setFunction(identString, (PostfixMathCommandI)jep.funTab.get(identString));
				ASTConstant leftarg = new ASTConstant(JJTCONSTANT);
				leftarg.setValue("|");
				jjtree.pushNode(leftarg);
			} else {
				addToErrorList(JEP.getMessage("JEP.ERR.UNREC_FUN") + ": \"" + identString + "\"");
			}
		}
		<PIPE> <LRND> ArgumentList(reqArguments, identString) <RRND>
	) #FunNode
	|
	LOOKAHEAD(20)							// added by LM: APL-style scan operator
	( identString = Identifier()
		{
			if(jep.funTab.containsKey(identString)) {
				reqArguments = ((PostfixMathCommandI)jep.funTab.get(identString)).getNumberOfParameters();
				jjtThis.setFunction(identString, (PostfixMathCommandI)jep.funTab.get(identString));
				ASTConstant leftarg = new ASTConstant(JJTCONSTANT);
				leftarg.setValue("\\");
				jjtree.pushNode(leftarg);
			} else {
				addToErrorList(JEP.getMessage("JEP.ERR.UNREC_FUN") + ": \"" + identString + "\"");
			}
		}
		<BDIV> <LRND> ArgumentList(reqArguments, identString) <RRND>
	) #FunNode
}


void ArgumentList(int reqArguments, String functionName) : { int count = 0; String errorStr = ""; }
{
	[
		Expression()
			{ count++; }
		( <COMMA> Expression()
			{ count++; }
		)*
	]
    	{
			if(reqArguments != count && reqArguments != -1) {
				errorStr = JEP.getMessage("JEP.ERR.FUN") + " \"" + functionName + "\" " + JEP.getMessage("JEP.ERR.REQ") + " " + reqArguments + " ";  // modified by lm
				if(reqArguments != 1) errorStr += JEP.getMessage("JEP.ERR.PARAMS");
				else errorStr += JEP.getMessage("JEP.ERR.PARAM");
				addToErrorList(errorStr);
			}
		}
}


String Identifier() : { Token t; }
{
	( t=<IDENTIFIER1> | t=<IDENTIFIER2> )
		{ return t.image; }
}


void AnyConstant() #Constant : { Token t; Object value; }
{
	t=<STRING_LITERAL>
		{
			// strip away double quotes at end of string
			String temp = (t.image).substring(1,t.image.length()-1);
			// replace escape characters
			temp = replaceEscape(temp);
			jjtThis.setValue(temp);
		}
	|
	value = RealConstant()
		{ jjtThis.setValue(value); }
}


Object RealConstant() : { Token t; Object value; }
{
	( t=<INTEGER_LITERAL> | t=<FLOATING_POINT_LITERAL> )
		{
			try {
				value = jep.getNumberFactory().createNumber(t.image);
			} catch (Exception e) {
				value = null;
				addToErrorList(JEP.getMessage("JEP.ERR.PARSE") + " \"" + t.image + "\""); // modified by lm
			}
			return value;
		}
}
